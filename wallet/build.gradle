import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlinx-serialization"
    id "org.jetbrains.kotlin.plugin.compose"  // Add this line
    id "com.google.protobuf" version "0.9.4"
}

def qtart_version = "0.13.10"

static def versionCodeEpoch() {
    return (new Date().getTime() / 1000).toInteger()
}
def gitCommit = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short=7', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    namespace 'net.taler.wallet'
    compileSdk 36

    defaultConfig {
        applicationId "net.taler.wallet"
        minSdkVersion 26
        targetSdkVersion 36
        versionCode 49
        versionName "0.14.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions = ["distributionChannel"]
    productFlavors {
        fdroid {
            dimension "distributionChannel"
            applicationIdSuffix ".fdroid"
        }
        google {
            dimension "distributionChannel"
        }
        nightly {
            dimension "distributionChannel"
            applicationIdSuffix ".nightly"
            versionCode versionCodeEpoch()
            versionNameSuffix " ($gitCommit)"
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    // REMOVE THIS SECTION - it's no longer needed with the Compose Compiler plugin
    // composeOptions {
    //     kotlinCompilerExtensionVersion "1.5.14"
    // }

    buildFeatures {
        buildConfig true
        viewBinding true
        compose true
    }
    packaging {
        jniLibs {
            useLegacyPackaging = true
        }
    }
    packagingOptions {
        jniLibs {
            keepDebugSymbols += ['**/*.so']
        }
        resources {
            excludes += ['META-INF/*.kotlin_module']
        }
    }
    splits {
        abi {
            enable project.hasProperty('splitApk')
            reset()
            include "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            universalApk false
        }
    }

    lint {
        abortOnError true
        ignoreWarnings false
    }
}

dependencies {
    implementation project(":taler-kotlin-android")
    implementation project(":transaction-database")
    implementation "net.taler:qtart:$qtart_version@aar"

    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "androidx.browser:browser:1.9.0"

    // Compose
    implementation platform('androidx.compose:compose-bom:2025.10.00')
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material:material-icons-extended'
    implementation "androidx.compose.runtime:runtime-livedata"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose"
    implementation "com.google.accompanist:accompanist-themeadapter-material3:0.28.0"
    implementation 'androidx.activity:activity-compose:1.11.0'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.ui:ui-viewbinding'
    implementation "androidx.fragment:fragment-compose:1.8.9"
    implementation 'androidx.privacysandbox.tools:tools-core:1.0.0-alpha13'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    // Lists and Selection
    implementation "androidx.recyclerview:recyclerview:1.4.0"
    implementation "androidx.recyclerview:recyclerview-selection:1.2.0"

    // Navigation Library
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    // QR codes
    implementation 'com.journeyapps:zxing-android-embedded:4.3.0@aar'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'

    // Nicer ProgressBar
    implementation 'me.zhanghai.android.materialprogressbar:library:1.6.1'

    // Markdown rendering
    final def markwon_version = '4.6.2'
    implementation "io.noties.markwon:core:$markwon_version"
    implementation "io.noties.markwon:ext-tables:$markwon_version"
    implementation "io.noties.markwon:recycler:$markwon_version"

    // Java Native Access
    implementation "net.java.dev.jna:jna:5.18.1@aar"

    // Shared preferences
    implementation "androidx.datastore:datastore:$datastore_version"
    implementation  "com.google.protobuf:protobuf-javalite:3.18.0"

    testImplementation "junit:junit:$junit_version"
    testImplementation 'org.json:json:20250517'
}

tasks.withType(KotlinCompile).configureEach {
    compilerOptions {
        freeCompilerArgs.add("-opt-in=kotlin.RequiresOptIn")
    }
}

ext.abiCodes = ['armeabi-v7a': 1, 'arm64-v8a': 2, x86: 3, x86_64: 4]
android.applicationVariants.configureEach { variant ->
    variant.outputs.each { output ->
        def baseAbiVersionCode =
                project.ext.abiCodes.get(output.getFilter(com.android.build.OutputFile.ABI))
        if (baseAbiVersionCode != null) {
            output.versionCodeOverride = 10 * variant.versionCode + baseAbiVersionCode
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:4.28.3"
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}