import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id("com.android.application")
    id("kotlin-android")
    id("kotlinx-serialization")
    id("com.google.protobuf") version "0.9.4"
    id("org.jetbrains.kotlin.plugin.compose") version "$kotlin_version"
}

def qtart_version = "1.0.34"

static def versionCodeEpoch() {
    return (new Date().getTime() / 1000).toInteger()
}
def gitCommit = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short=7', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    namespace 'net.taler.wallet'
    compileSdk 35

    defaultConfig {
        applicationId "net.taler.wallet"
        minSdkVersion 26
        targetSdkVersion 35
        versionCode 68
        versionName "OIM-v0.1.0-tʋɩɩse"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions = ["distributionChannel"]
    productFlavors {
        fdroid {
            dimension "distributionChannel"
            applicationIdSuffix ".fdroid"
        }
        google {
            dimension "distributionChannel"
        }
        nightly {
            dimension "distributionChannel"
            applicationIdSuffix ".nightly"
            versionCode versionCodeEpoch()
            versionNameSuffix " (${gitCommit()})"
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    buildFeatures {
        buildConfig true
        viewBinding true
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging = false
        }
        resources {
            excludes += ['META-INF/*.kotlin_module']
            excludes += ['META-INF/DEPENDENCIES']
            excludes += ['META-INF/LICENSE*']
            excludes += ['META-INF/NOTICE*']
            excludes += ['DebugProbesKt.bin']
            excludes += ['kotlin/**']
            excludes += ['**.properties']
        }
    }

    splits {
        abi {
            enable true
            reset()
            include "armeabi-v7a", "arm64-v8a", "x86", "x86_64"  // All architectures
            universalApk false
        }
    }

    lint {
        abortOnError true
        ignoreWarnings false
        checkGeneratedSources false  // ADDED: Ignore lint in generated code
    }
}

dependencies {
    api project(":transaction-database")
    api project(":taler-kotlin-android")

    implementation "net.taler:qtart:$qtart_version@aar"
    implementation("io.coil-kt:coil-compose:2.4.0") {
        exclude group: 'androidx.lifecycle', module: 'lifecycle-viewmodel-ktx'
    }

    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "androidx.browser:browser:1.8.0"
    implementation "androidx.biometric:biometric-ktx:1.4.0-alpha02"

    // Compose
    implementation platform('androidx.compose:compose-bom:2025.05.00')
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material:material-icons-extended'
    implementation "androidx.compose.runtime:runtime-livedata"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose"
    implementation "com.google.accompanist:accompanist-themeadapter-material3:0.28.0"
    implementation 'androidx.activity:activity-compose:1.9.3'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.ui:ui-viewbinding'
    implementation "androidx.fragment:fragment-compose:1.8.6"
    debugImplementation 'androidx.compose.ui:ui-tooling'

    // Lists
    implementation "androidx.recyclerview:recyclerview:1.4.0"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    // QR codes
    implementation 'com.journeyapps:zxing-android-embedded:4.3.0@aar'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
    implementation 'com.google.zxing:core:3.5.2'

    // Progress bar
    implementation 'me.zhanghai.android.materialprogressbar:library:1.6.1'

    // Markdown rendering
    final def markwon_version = '4.6.2'
    implementation "io.noties.markwon:core:$markwon_version"
    implementation "io.noties.markwon:ext-tables:$markwon_version"
    implementation "io.noties.markwon:recycler:$markwon_version"

    // JNA
    implementation "net.java.dev.jna:jna:5.17.0@aar"

    // DataStore
    implementation "androidx.datastore:datastore:$datastore_version"
    implementation "com.google.protobuf:protobuf-javalite:3.18.0"

    // Testing
    testImplementation "junit:junit:$junit_version"
    testImplementation 'org.json:json:20220320'
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }
}

// All architectures including x86 for emulators
ext.abiCodes = ['armeabi-v7a': 1, 'arm64-v8a': 2, x86: 3, x86_64: 4]
android.applicationVariants.configureEach { variant ->
    variant.outputs.each { output ->
        def baseAbiVersionCode = project.ext.abiCodes.get(output.getFilter(com.android.build.OutputFile.ABI))
        if (baseAbiVersionCode != null) {
            output.versionCodeOverride = 10 * variant.versionCode + baseAbiVersionCode
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:4.28.3"
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}