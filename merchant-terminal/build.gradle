plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlinx-serialization'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    namespace 'net.taler.merchantpos'
    compileSdk 34

    defaultConfig {
        applicationId "net.taler.merchantpos"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 9
        versionName "0.2.7"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "BACKEND_API_VERSION", "\"5:0:1\"")
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled minify_debug
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    buildFeatures {
        buildConfig = true
        viewBinding = true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/common.kotlin_module', 'META-INF/*.kotlin_module']
        }
    }

    lint {
        abortOnError true
        disable 'WrongConstant'
        ignoreWarnings false
    }
}

dependencies {
    implementation project(":merchant-lib")
    implementation project(":taler-kotlin-android")

    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "androidx.recyclerview:recyclerview:1.3.2"
    implementation "androidx.recyclerview:recyclerview-selection:1.1.0"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    testImplementation 'androidx.test.ext:junit:1.2.1'
    testImplementation 'org.robolectric:robolectric:4.10.3'
}
